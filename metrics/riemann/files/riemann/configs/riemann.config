; -*- mode: clojure; -*-
; vim: filetype=clojure

;Set up a log file for Riemann to write to:
(logging/init {:file "/var/log/riemann/riemann.log"})

; Listen on the local interface over TCP (5555), UDP (5555), and websockets
; (5556)
(let [host "0.0.0.0"]
  (tcp-server {:host host})
  (udp-server {:host host})
  (ws-server  {:host host}))

;Start a Graphite server on the usual TCP port for Carbon, port 2003.
(def graphite-server-tcp (graphite-server :host "0.0.0.0"
                 :protocol :tcp
                 :parser-fn
                  ; This function parses Graphite events after they've been pre-processed by (graphite-server) calling (decode-graphite-line).
                  ;They start out looking like this, with just the :service, :metric and :time keys having values:
                  ; #riemann.codec.Event{
                  ;:host nil,
                  ;:service collectd.collectd1_local.df-run.df_complex-reserved,
                  ;:state nil,
                  ;:description nil,
                  ;:metric 0.0,
                  ;:tags nil,
                  ;:time 1402720236,
                  ;:ttl nil}

                  ;The :time and :metric keys are set the way we want them to be, but we have to break up the value of the :service key into smaller
                  ;useful pieces. Below is the function that does this.

                  ;We start by getting the :service key and setting it to the local value 'service'. The :keys [service] is what does this. If we
                  ;wanted to, we could also grab other keys by listing them after service: [service metric state ttl]. The ':as' lets us 
                  ;refer to the original event as 'event' within the function.
                 (fn [{:keys [service] :as event}]
                   ;The let statement creates the local values 'source', 'hostname' and 'metricname'. The split function call splits the 'service'
                   ; string when it encounters a period, '.' which gets specified by the '#"\."', which is a Clojure regular expression. The last
                   ;argument to the split function, '3', specifies how many splits to do at the most. Since the original :service value looks like this:

                   ; 'collectd.collectd1_local.df-run.df_complex-reserved', we'll get 'collectd', 'collectd1_local' and 'df-run.df_complex-reserved'

                   ;Splitting more than 3 times would turn 'df-run.df_complex-reserved' into 'df-run' and 'df_complex-reserved', when they're really
                   ;smaller pieces of the same metric name. The [source hostname metricname] vector before the split function call tells Riemann to take
                   ;the first result of the split and put it in the 'source' value, the 2nd result of the split into the 'hostname' value and the 3rd
                   ;result into the 'metricname' value.

                   (let [[source hostname metricname] (clojure.string/split service #"\." 3)]
                    {
                    ;Once we've split the :service key's value and put it into the 3 local values, we can create a new Riemann event and fill in
                    ;some of the missing values.
                    ; Set the :host key to the value of the hostname
                      ; The replace function finds _ in the hostnames and replaces them with a period. collectd adds _ in place of . for hostnames
                      ; since it uses . to separate out the hostname from the metric name
                    :host (clojure.string/replace hostname #"_" ".")
                    ;set the service key of the event to the metricname that we parsed out above
                    :service metricname
                    ;set the :metric key of the new event to the value of the :metric key in the old event.
                    ;In Clojure, map keys can be called as functions and given the map as an argument; the key's value is returned.
                    ;We do that here to grab the :metric value out of the old event and set it equal to the :metric value in the new event:
                    :metric (:metric event)
                    ;This works like the :service metricname above
                    :tags source
                    ;This works in the same way as setting the :host above
                    :time (:time event)
                    ;Keep the returned event in the index for 30 seconds
                    :ttl 30}))))
                    ;The end result of the above is this:
                    ; #riemann.codec.Event{:host nil, :service collectd.collectd1_local.df-run.df_complex-reserved, :state nil, :description nil, :metric 0.0, :tags nil, :time 1402720236, :ttl nil}
                    ; ...getting turned into this:
                    ; #riemann.codec.Event{:host collectd1.local, :service df-root.df_complex-reserved, :metric 3.3860608E9, :tags collectd, :time 1402877026, :ttl 30}

;Like the graphite-server-tcp function above, but listening on for UDP packets instead of TCP:
(def graphite-server-udp (graphite-server :host "0.0.0.0"
                 :protocol :udp
                 :parser-fn
                 (fn [{:keys [service] :as event}]
                   (let [[source hostname metricname] (clojure.string/split service #"\." 3)]
                    { :host (clojure.string/replace hostname #"_" ".")
                      :service metricname
                      :metric (:metric event)
                      :tags source
                      :time (:time event)
                      :ttl 30}))))

; Expire old events from the index every 30 seconds.
(periodically-expire 30)

;My old, synchronous function for writing to InfluxDB.
;WARNING: DO NOT USE THIS FUNCTION. It will create a new socket to InfluxDB for EVERY event that gets written,
;which is obviously bad for performance reasons:
(def write-influxdb (influxdb
    { :host "10.0.1.195"
      :port 8086
      :scheme "http"
      :username "nick"
      :password "password"
      :db "riemann-data"
      :series #(str (:host %) "." (:service %))}))


;The following code is based on the code sample in this pull request comment on Github:
; https://github.com/aphyr/riemann/issues/411#issue-36716498

(require 'capacitor.core)
(require 'capacitor.async)
(require 'clojure.core.async)

;Create an asynchronous InfluxDB client using the capacitor library:
(defn make-async-influxdb-client [opts]
    (let [client (capacitor.core/make-client opts)
      ;Make a channel to buffer influxdb events
      events-in (capacitor.async/make-chan)
      ;Make a channel to collect influxdb responses (ignored, really)
      resp-out (capacitor.async/make-chan)]
      ;Start the run loop with a batch size of max 100 events and max 10 seconds
      (capacitor.async/run! events-in resp-out client 100 10000)
        (fn [series payload]
          (let [p (merge payload {
            :series series
            ;; s â†’ ms
            :time   (* 1000 (:time payload))})]
            (clojure.core.async/put! events-in p)))))

;A function that takes the asynchronnous InfluxDB client created above and creates a function we can use in our streams
; to write to InfluxDB asynchronously:
(def write-influxdb-async (make-async-influxdb-client {
  :host "10.0.1.195"
  :port 8086
  :username "nick"
  :password "password"
  :db "riemann-data"
  }))

(let [index (index)]

  ;Inbound events will be passed to the functions that come after (streams...
  (streams
    ;This is one function. Index all events immediately:
    index

    ;Asynchronous InfluxDB writer; this creates series names automatically by combining the hostname and service name from the event
    ;passed into it:
    (fn [event]
      (let [series (format "%s.%s" (:host event) (:service event))]
        (write-influxdb-async series {
          :host (:host event)
          :time  (:time event)
          :value (:metric event)
          :name (:service event)})))

    ;Log everything to the log file defined above in (logging/init...
    ;Commenting it out since we don't need it right now, but it's nice to have at hand in case we need to debug things later:
    ;#(info %)
    ))